# Fixed PKGBUILD from https://github.com/stefanpartheym/archlinux-ipu6-webcam
# Removed the udev rule that creates bunch of broken symlinks
# Original PKGBUILD: https://aur.archlinux.org/packages/intel-ipu6ep-camera-hal-git
_ipu_ver=ipu6ep
pkgname=intel-ipu6ep-camera-hal-git-fix
_pkgname=ipu6-camera-hal
pkgver=r126.c933525
pkgrel=1
pkgdesc="Intel IPU6 camera HAL (Alder Lake)"
arch=('x86_64')
url="https://github.com/intel/ipu6-camera-hal"
license=('unknown')
depends=('intel-ipu6-dkms-git' 'intel-ipu6ep-camera-bin')
makedepends=('git' 'cmake')
provides=(ipu6-camera-hal)
conflicts=('intel-ipu6-camera-hal-git' 'intel-ipu6ep-camera-hal-git')
source=("git+${url}.git#commit=c933525a6efe8229a7129b7b0b66798f19d2bef7")
sha256sums=('SKIP')

prepare() {
    cd $_pkgname
    sed -i "s|-D_FORTIFY_SOURCE=2| |" CMakeLists.txt
}

pkgver() {
    cd $_pkgname
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
    cmake -B build -S "$_pkgname"       \
        -DCMAKE_BUILD_TYPE=Release      \
        -DIPU_VER=$_ipu_ver             \
        -DUSE_PG_LITE_PIPE=ON           \
        -DCMAKE_INSTALL_PREFIX="/usr" \
        -DCMAKE_POLICY_VERSION_MINIMUM=3.5 \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DBUILD_CAMHAL_ADAPTOR=ON \
        -DBUILD_CAMHAL_PLUGIN=ON \
        -DIPU_VERSIONS="ipu6;ipu6ep;ipu6epmtl"
    cmake --build build
}

package() {
    DESTDIR="$pkgdir" cmake --install build
}
