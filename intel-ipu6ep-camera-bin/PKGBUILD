# Fixed PKGBUILD from https://github.com/stefanpartheym/archlinux-ipu6-webcam
# Fixed paths and added symlinks to fix the build
# Original PKGBUILD: https://aur.archlinux.org/packages/intel-ipu6ep-camera-bin
pkgbase=ipu6-camera-bin
pkgname=intel-ipu6ep-camera-bin-fix
_pkgname=ipu6-camera-bins
pkgver=r92.30e8766
pkgrel=1
pkgdesc="Intel IPU6 camera binaries (Alder Lake)"
arch=('x86_64')
url="https://github.com/intel/ipu6-camera-bins"
license=('unknown')
depends=('intel-ipu6-dkms-git' 'glibc')
makedepends=('git')
provides=(ipu6-camera-bin intel-ipu6ep-camera-bin)
conflicts=(intel-ipu6-camera-bin intel-ipu6ep-camera-bin)
source=("git+${url}.git#commit=30e87664829782811a765b0ca9eea3a878a7ff29")
sha256sums=('SKIP')

pkgver() {
    cd $_pkgname
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
    install -dm755 "$pkgdir/usr"
    rm -rf "$_pkgname/lib/firmware/"
    for lib in $_pkgname/lib/lib*.so.*; do \
        lib=${lib##*/}; \
        ln -s $lib "$_pkgname/lib/${lib%.*}"; \
    done
    cp -rT "$_pkgname/lib" "$pkgdir/usr/lib"
    cp -rT "$_pkgname/include" "$pkgdir/usr/include"
}
