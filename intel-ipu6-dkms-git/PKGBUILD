# Fixed PKGBUILD from https://github.com/stefanpartheym/archlinux-ipu6-webcam
# Pinned upstream driver version to a specific commit
# Original PKGBUILD: https://aur.archlinux.org/packages/intel-ipu6-dkms-git
pkgbase=intel-ipu6-dkms-git
pkgname=intel-ipu6-dkms-git-fix
_pkgname=ipu6-drivers
pkgver=r236.9bff73689
pkgrel=1
pkgdesc="Intel IPU6 camera drivers (DKMS)"
arch=('any')
url="https://github.com/intel/${_pkgname}"
license=('unknown')
depends=('dkms' 'intel-ivsc-firmware')
provides=('intel-ivsc-driver-dkms-git' 'intel-ipu6-dkms-git')
conflicts=('intel-ivsc-driver-dkms-git' 'intel-ipu6-dkms-git')
makedepends=('git')
source=("git+${url}#commit=9bff73689ea2502f6e3bc34769fd699cde3ffeea")
sha256sums=('SKIP')

pkgver() {
    cd $_pkgname
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
    cd "$srcdir/$_pkgname"
    sed -i "s/^PACKAGE_VERSION=\".*\"$/PACKAGE_VERSION=\"$pkgver\"/g" ./dkms.conf
}

package() {
    cd "$srcdir"
    install -dm755 "$pkgdir/usr/src"
    cp -rT "$_pkgname" "$pkgdir/usr/src/$_pkgname-$pkgver"
}
